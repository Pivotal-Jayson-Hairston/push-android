// Top-level build file where you can add configuration options common to all sub-projects/modules.

////////////////////////////////////////////////////////////////
//// Ensure your ~/.gradle/gradle.properties file or environment defines the following properties:
// MAVEN_USER = your user name on the above Artifactory.
// MAVEN_PASS = your password on the above Artifactory.  It is encourage to use the encrypted form of your password
////////////////////////////////////////////////////////////////

//// Set your application versionName and versionCode here. ////
ext {
    versionName = "0.6"
    versionCode = 6
    isSnapshot = true
    
    libraryProjects = [
        project(":Push")
    ]
    applicationProjects = [
        project(":Push-Sample"),
        project(":Push-Demo")
    ]
}
////////////////////////////////////////////////////////////////

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.11.+'
    }
}

allprojects {
    apply plugin: 'maven'

    group = "io.pivotal.android"

    if (isSnapshot) {
        version =  versionName + "-SNAPSHOT"
    } else {
        version =  versionName
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://maven.xtremelabs.com/artifactory/xl-libs'
            credentials {
                username MAVEN_USER
                password MAVEN_PASS
            }
        }
    }
}

// LIBRARIES
configure (libraryProjects) {

    apply plugin: 'android-library'

    android {
        compileSdkVersion 19
        buildToolsVersion "19.1.0"

        defaultConfig {
            versionCode versionCode
            versionName versionName
            minSdkVersion 10
            targetSdkVersion 19
        }
    }

    configurations {
        archives {
            extendsFrom configurations.default
        }
    }

    uploadArchives {
        configuration = configurations.archives
        repositories.mavenDeployer {
            repository(url: 'https://maven.xtremelabs.com/artifactory/libs-release-local') {
                authentication(userName: MAVEN_USER, password: MAVEN_PASS)
            }
            snapshotRepository(url: 'https://maven.xtremelabs.com/artifactory/libs-snapshot-local') {
                authentication(userName: MAVEN_USER, password: MAVEN_PASS)
            }
        }
    }

    ext.pomInfo = {
        packaging 'aar'
        url 'https://github.com/cfmobile/push-android'

        scm {
            url 'git@github.com:cfmobile/push-android.git'
            connection 'scm:git:git@github.com:cfmobile/push-android.git'
            developerConnection 'scm:git:git@github.com:cfmobile/push-android.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'rszumlakowski'
                name 'Robert Szumlakowski'
                email 'robsz@pivotallabs.com'
            }
        }
    }
}

// APPLICATIONS
configure (applicationProjects) {

    apply plugin: 'android'

    android {
        compileSdkVersion 19
        buildToolsVersion "19.1.0"

        defaultConfig {
            versionCode versionCode
            versionName versionName
            minSdkVersion 10
            targetSdkVersion 19
        }
    }
}
